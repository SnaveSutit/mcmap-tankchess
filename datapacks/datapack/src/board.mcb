import ./const.mcbm

function apply_size_changes {
	# Save the last size
	scoreboard players operation board.last.x v = board.x v
	scoreboard players operation board.last.z v = board.z v
	# Apply the new size
	scoreboard players operation board.x v = board.new.x v
	scoreboard players operation board.z v = board.new.z v
}

function build_main {
	execute positioned 0 10 0 run {
		function board:build_local
	}
}

function build_editor {
	execute positioned 1000 10 0 run {
		function board:build_local
	}
	# fill 990 5 -10 1011 5 11 piston[facing=up]
	# fill 991 5 -9 1009 5 9 air
}

function build_local {
	function board:remove_previous
	# Center board in play area
	scoreboard players operation #x v = board.x v
	scoreboard players operation #x v /= 2 v
	{
		# Center x
		scoreboard players remove #x v 1
		execute if score #x v matches 0 run {
			# Center z
			scoreboard players operation #z v = board.z v
			scoreboard players operation #z v /= 2 v
			{
				scoreboard players remove #z v 1
				execute if score #z v matches 0 run function board:place_checkerboard
				execute if score #z v matches 1.. positioned ~ ~ ~-1 run function $block
			}
		}
		execute if score #x v matches 1.. positioned ~-1 ~ ~ run function $block
	}
}

function place_checkerboard {
	# Loop over z
	scoreboard players operation #z v = board.z v
	{
		scoreboard players operation #x v = board.x v
		# Loop over x
		{
			# eq #a v = (#x v + #z v) % 2
			scoreboard players operation #a v = #x v
			scoreboard players operation #a v += #z v
			scoreboard players operation #a v %= 2 v
			execute if score #a v matches 0 run {
				setblock ~ ~ ~ white_glazed_terracotta
			}
			execute if score #a v matches 1 run {
				setblock ~ ~ ~ orange_glazed_terracotta
			}

			execute if score #x v = board.x v run setblock ~-1 5 ~ piston[facing=up]
			execute if score #x v matches 1 run setblock ~1 5 ~ piston[facing=up]

			execute if score #z v = board.z v run setblock ~ 5 ~-1 piston[facing=up]
			execute if score #z v matches 1 run setblock ~ 5 ~1 piston[facing=up]

			scoreboard players remove #x v 1
			execute if score #x v matches 1.. positioned ~1 ~ ~ run function $block
		}
		scoreboard players remove #z v 1
		execute if score #z v matches 1.. positioned ~ ~ ~1 run function $block
	}
}

function remove_previous {
	fill 990 5 -10 1011 5 11 air
	# Center board in play area
	scoreboard players operation #x v = board.last.x v
	scoreboard players operation #x v /= 2 v
	{
		# Center x
		scoreboard players remove #x v 1
		execute if score #x v matches 0 run {
			# Center z
			scoreboard players operation #z v = board.last.z v
			scoreboard players operation #z v /= 2 v
			{
				scoreboard players remove #z v 1
				execute if score #z v matches 0 run function board:remove_checkerboard
				execute if score #z v matches 1.. positioned ~ ~ ~-1 run function $block
			}
		}
		execute if score #x v matches 1.. positioned ~-1 ~ ~ run function $block
	}
}

function remove_checkerboard {
	# Loop over z
	scoreboard players operation #z v = board.last.z v
	{
		scoreboard players operation #x v = board.last.x v
		# Loop over x
		{
			setblock ~ ~ ~ air

			scoreboard players remove #x v 1
			execute if score #x v matches 1.. positioned ~1 ~ ~ run function $block
		}
		scoreboard players remove #z v 1
		execute if score #z v matches 1.. positioned ~ ~ ~1 run function $block
	}
}

